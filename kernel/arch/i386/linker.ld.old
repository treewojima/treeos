/* Symbol to use as the entry point for the kernel executable */
ENTRY(_start)

/* Virtual memory/higher half offsets */
KERNEL_BOOT_VMA = 0x00100000;
/*KERNEL_HIGH_VMA = 0xC0000000;*/
KERNEL_HIGH_VMA = 0;

/* Tell where the various sections of the object files will be put in the final
 * kernel image. */
SECTIONS
{
    /* Start off at the low physical address */
    . = KERNEL_BOOT_VMA;
    g_kernel_init_start = . ;

    /* Multiboot header and bootstrap .text */
    .boot :
    {
        *(.multiboot)
        */boot.o(.text) /* Only put the section from boot.S/o in here */
    }

    /* Skip ahead to the higher half address */
    . = ALIGN(0x1000);
    g_kernel_init_end = . ;
    . += KERNEL_HIGH_VMA;
    g_kernel_start = . ;

    /* The regular kernel text section */
    .text ALIGN(0x1000) : AT(ADDR(.text) - KERNEL_HIGH_VMA)
    {
        *(EXCLUDE_FILE(*/boot.o) .text) /* exclude the boot.S/o .text */
    }

    /* Read-only data */
    .rodata ALIGN(0x1000) : AT(ADDR(.rodata) - KERNEL_HIGH_VMA)
    {
        *(.rodata)
    }

    /* Read-write data (initialized) */
    .data ALIGN(0x1000) : AT(ADDR(.data) - KERNEL_HIGH_VMA)
    {
        *(.data)
    }

    /* Read-write data (uninitialized) and stack */
    /*.bss BLOCK(0x1000) : ALIGN(0x1000)*/
    .bss ALIGN(0x1000) : AT(ADDR(.bss) - KERNEL_HIGH_VMA)
    {
        g_bss_start = . ;
        
        *(COMMON)
        *(.bss)
        *(.bootstrap_stack)

        /* Align/pad to the nearest 4K page boundary */
        . = ALIGN(0x1000);
        g_bss_end = . ;
    }

    /* Page alignment is guaranteed for this symbol */
    g_kernel_end = . ;
}
